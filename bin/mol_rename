#!/usr/bin/perl -w

#  COPYRIGHT NOTICE
#  
#  Silico - A Perl Molecular Toolkit
#  Copyright (C) 2008-25 David K. Chalmers and Benjamin P. Roberts,
#  Department of Medicinal Chemistry, Monash University
#  
#  This program is free software: you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#  
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#  
#  You should have received a copy of the GNU General Public License
#  along with this program.  If not, see <http://www.gnu.org/licenses/>.


#<
#! mol_rename
#? Rename molecules 
#. Can also:
#, change the molecule name to the filename
#, change the molecule filename to the molecule name
#, rename the molecule using a specified SDF data field
#. Using both the -r<sdf_datafield> and -c flags can be used to change 
#  filename to the specified SDF data field.
#. Can generate 'insight_safe' names.  i.e. So that they do not contain spaces,
#  punctuation, start with an underscore or a digit and are of limited length.
#
#F
#!3 Options that modify the molecule name
#; -b_<name>           Set the molecule name to <name> (can be also used as a molecule base name)
#; -a_<suffix>          Add <suffix> to the end of the existing molecule name
#; -f		Change molecule name to filename	
#; -r_<field_name>	Rename molecules using the <field_name> SDF Data field.  
#; -smiles              Renamie molecules using SMILES string
#
#. These flags modify the molecule name in the CT record of SDF files
#. These three flags can be combined.  The changes will be concatenated in the order -b, -f, -r
#
#. Options the modify the existing molecule name
#; -safe	Use insight-safe names (ie that do not contain spaces, punctuation, start with an underscore or a digit  and are of limited length)
#; -n 		Add a number to the end of the name
#. These flags can be specified in conjunction with the flags above
#
#!3 Options that modify molecule data (SDF data)
#
#; -s		Transfer the molecule name to the SDF Data fields 'NAME' and 'title'
#; -sdfield_<field_name> Change specified SDF field to molecule name
#; -sddel_<"field_field">	 Delete SDF field
#
#!3 Options that make multiple changes to the molecule name
#; -g_<base>	Generate new name using <base>, add a number and change SDF Name field (same as -b, -n, -s)
#
#!3 Options that modify the file name
#; -c		Change output filename to name of first molecule
#
#!3 Other options
#; -k		Keep the same filename, overwriting the input file (possibly dangerous)
#; -np		Use noparse option (much faster, but currently only available for sdf files)
#
#SF
#. Created: DKC 2000-2014
#. $Revision: 1.37.2.5.2.9 $
#>

use strict;
package Silico;

#########################
# Variable declarations #
#########################

my $namehash;
my $offset = 0;
my $starttime = (times)[0];

%$namehash = ();

###################
# Start of script #
###################

silico_setup();
print_title("mol_rename", '$Revision: 1.37.2.5.2.9 $');

# Multiple changes
my $genname =	make_flag('g', 'generate-name', "Generate new name using supplied base, add a number and change SDF 'NAME' field (same as -b, -n, -s)", 1);

# Options that set the molecule name
my $setname =	make_flag('b', 'set-name', "Set this molecule name (or base name)", 1);
my $suffix = 	make_flag('a', 'set-suffix', "Add this to end of existing name", 1);
my $fname =	make_flag('f', 'change-molname-to-filename', "Change molecule name to filename");
my $rename =	make_flag('r', 'change-molname-to-SDF-data', "Rename molecules using SDF data field", 1, undef);
my $smiles =    make_flag('smiles', 'change-molname-to-smiles', "Rename molecules to SMILES string");

# Options that modify the existing molecule name
my $safe =	make_flag('safe', 'insight-safe', "Use insight-safe names");
my $addnumber =	make_flag('n', 'add-number', "Add a number to the end of name");

# Options that modify SDF data
my $sname =	make_flag('s', 'change-SDF-NAME-to-molname', "Change SDF 'NAME' field to molname");
my $sdfield = 	make_flag('sdfield', 'change-SDF-field-to-name', "Change specified SDF field to name", 1, undef);
my $sddelete = 	make_flag('sddel', 'delete-SDF-field', "SDF data fields to delete", 1, undef);

# Options that change the filename
my $keep =	make_flag('k', 'keep-filename', "Keep same filneame");
my $changefile = make_flag('c', 'change-filename-to-molname', "Change output filename to name of first molecule");

my $options = 'QUIET';
$options .= ' NOPARSE' if make_flag('np', 'noparse', "Use noparse option (available for sdf files)");

# Default separator and number of leading zeros for for molecule numbering: Mol_276
my $sep = "_";
my $lead = 4;

my @del_list;
if ($sddelete) {

        @del_list = split " ", $sddelete;
        print "Deleting fields: ";
        foreach (@del_list) {
                print "'$_' ";
        }
        print "\n";
}

if (get_sflag('g')) {

	$sname = 1;
	$addnumber = 1;
	$setname = $genname;
}

my @arguments = get_arguments2('>=1');

silico_msg('d', "No renaming method has been selected! Nothing to do.\n") if 
	(!$changefile && !$rename && !$fname && !$safe && !$sname && !$setname && !$addnumber);

my $molcount = 0;


foreach my $arg (@arguments) {
		
	my $filebase = get_filebase($arg);
		
	silico_msg('c', "File: $arg\n");
	
	my $mols = read_mol_any($arg, undef, undef, $options);
	if (!defined $mols->[0]) {
		mol_read_error($arg);
		next;
	}
	
	my $oformat = get_oformat($mols);
	my $molnum = 0; 
		
	foreach my $mol (@$mols) {
	
		++$molcount;
		++$molnum;
		$mol->{NAME} ||= 'Mol';

		silico_msg('c', "  Mol: '$mol->{NAME}'");
		
		my $oname = $mol->{NAME};
		
		# Options that rename the molecule 
		# Can be used in conjunction
		$mol->{NAME} = '' if $setname || $fname || $rename;
		
		if ($setname) {
			$mol->{NAME} .= $setname || "Mol";	
		} 
		
		if ($fname) {
			$mol->{NAME} .= $filebase;
		} 
		
		if ($smiles) {
		
			$mol->{NAME} = mol_smiles_string($mol);
		}
		
		if ($rename) {
		
			if (!defined $mol->{SDF_DATA}{$rename}) {
				silico_msg ('w', "SDF data field \"$rename\" was not defined for molecule $molcount. Using \"Mol_$molcount\".\n");
				$mol->{NAME} .= "Mol_$molcount";
			} else {
				$mol->{NAME} .= $mol->{SDF_DATA}{$rename};
			}
		}
		
		# Options that modify the existing molecule name
		if ($addnumber) {
			$mol->{NAME}.= sprintf("$sep%0$lead"."d", $molnum+$offset);
		}
		
		# Add suffix
		if ($suffix) {
			$mol->{NAME} .= $suffix;
		}
		
		# Make insight-safe names
		if ($safe) {
			$mol->{NAME} = insight_rename($mol->{NAME}, $namehash);
		}
		
		silico_msg('c', " renamed to '$mol->{NAME}'") if ($mol->{NAME} ne $oname);
		silico_msg('c', "\n");
		
		# Options that modify SDF data
		# Set the SDF field 'NAME' to the molecule name
		if ($sname) {
		
			silico_msg('c', "        Setting SDF 'NAME' and 'title' fields to '$mol->{NAME}'\n");
			$mol->{SDF_DATA}{NAME} = $mol->{NAME};
			$mol->{SDF_DATA}{title} = $mol->{NAME};
			$mol->{name} =  $mol->{NAME} if defined $mol->{name};
		}
		
		# Set specified SDF field to the molecule name
		if ($sdfield) {
			$mol->{SDF_DATA}{$sdfield} = $mol->{NAME};
		}

		foreach (@del_list) {
			delete $mol->{SDF_DATA}{$_};
		}
	}
	
	my $obase;
	if ($keep) {
		# Keep existing filename
		$obase = $filebase;
	} elsif ($changefile) {
	
		# Change filename to moleculename if changefile is selected
		$obase = $mols->[0]{NAME};
		$obase =~ s/ /_/g;
	} else {
		
		# Append '_ren'
		$obase = $filebase.'_ren';
	}
	
	write_mol_any($mols, $obase, $oformat, 'QUIET');
}

print_finished_message();
print_timing_message($starttime);

#################
# End of script #
#################






###############
# Subroutines #
###############

sub silico_setup {

        #<
        #? Locate the Silico libraries and read in the parent library, silico.pm
        #; Sets: $Silico::home_dir, $Silico::lib_dir
        #; Requires: nothing
        #; Returns: nothing
        #>

        die "Error: The environment variable SILICO_HOME is not set\n" if !$ENV{SILICO_HOME};

        $Silico::home_dir = $ENV{SILICO_HOME}."/";
        $Silico::lib_dir = $Silico::home_dir."lib/";
        push @INC, substr($Silico::lib_dir, 0, -1);

        require silico;
}
