#!/usr/bin/perl -w

#  COPYRIGHT NOTICE
#  
#  Silico - A Perl Molecular Toolkit
#  Copyright (C) 2008-25 David K. Chalmers and Benjamin P. Roberts,
#  Department of Medicinal Chemistry, Monash University
#  
#  This program is free software: you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#  
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#  
#  You should have received a copy of the GNU General Public License
#  along with this program.  If not, see <http://www.gnu.org/licenses/>.


#<
#! mol_charge
#? Find the total partial and formal charges on molecle
#F
#; -f Calculate formal charges
#; -p Print atoms with formal charges
#; -df Die if formal charge is not zero
#; -r Provide a charge breakdown by residue
#; -s Provide a charge breakdown by segment
#SF
#. $Revision: 1.15-59-g89487b8 $
#>

use strict;
package Silico;

#########################
# Variable declarations #
#########################

my $starttime = (times)[0];

###################
# Start of script #
###################

silico_setup();
print_title("mol_charge", '$Revision: 1.15-59-g89487b8 $');

my $formal =	make_flag('f', 'formal',  'Calculate formal charges');
my $print =     make_flag('p', 'print-formal',  'Print atoms with formal charges');
my $df =        make_flag('df', 'die-chg-not-zero',  'Die if formal charge on system is not zero');
my $r = 	make_flag('r', undef,  'Provide a charge breakdown by residue');
my $s =	make_flag('s', undef,  'Provide a charge breakdown by segment');

my @arguments = get_arguments2('>=1');

set_default_oappend('cc');

foreach my $arg (@arguments) {

	silico_msg('c', "File: $arg\n");
	my $mols = read_mol_any($arg);
	my $filebase = get_filebase($arg);
	my $ext = get_fileext($arg);
	if (!defined $mols->[0]) {
		mol_read_error($arg);
		next;
	}
	
	foreach my $mol (@$mols) {
	
		molecule_check_and_fix_connectivity($mol);

		if ($formal || $df) {
			
			my $formal_charge = molecule_formal_charge($mol, undef, undef, $print);
			
			$mol->{SDF_DATA}{FORMAL_CHARGE} = $formal_charge;
			printf "Molecule: $mol->{NAME}\tFormal charge: %4.2f\n", $formal_charge;
			
			if ($df && $formal_charge != 0) {
				silico_msg('d', "The formal charge on this system is not zero. Dying (as requested)\n");
			}
		
		} else {
		
			my $tot_partial_charge = mol_calc_charge($mol, $r, $s);

			if (check_data_type ($tot_partial_charge, 'DECIMAL', 1) == 1 && $tot_partial_charge != int ($tot_partial_charge)) {
				
				silico_msg('w', "Overall charge of molecule \"$mol->{NAME}\" is not integral ($tot_partial_charge).\n",
					"Charge calculated as: $tot_partial_charge\n");

				$mol->{SDF_DATA}{CHARGE} = $tot_partial_charge;
				$tot_partial_charge = sprintf "%4.3f", $tot_partial_charge;
			}
			
			printf "Molecule: $mol->{NAME}\tTotal partial charge: %s\n", $tot_partial_charge;
		}
	}
	
	write_mol_any($mols);
}

print_finished_message();
print_timing_message($starttime);

#################
# End of script #
#################






###############
# Subroutines #
###############

sub silico_setup {

	#<
        #? Locate the Silico libraries and read in the parent library, silico.pm
        #; Sets: $Silico::home_dir, $Silico::lib_dir
        #; Requires: nothing
        #; Returns: nothing
        #>

        die "Error: The environment variable SILICO_HOME is not set\n" if !$ENV{SILICO_HOME};

        $Silico::home_dir = $ENV{SILICO_HOME}."/";
        $Silico::lib_dir = $Silico::home_dir."lib/";
        push @INC, substr($Silico::lib_dir, 0, -1);

        require silico;
}
