#!/usr/bin/perl -w

#  COPYRIGHT NOTICE
#  
#  Silico - A Perl Molecular Toolkit
#  Copyright (C) 2008-25 David K. Chalmers and Benjamin P. Roberts,
#  Department of Medicinal Chemistry, Monash University
#  
#  This program is free software: you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#  
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#  
#  You should have received a copy of the GNU General Public License
#  along with this program.  If not, see <http://www.gnu.org/licenses/>.


#<
#! file_rename
#? Rename files using a perl regular expression substitution
#. For example file_rename -a _new <filename> will replace the string '_new' with '' and the
#  file clozapine_new.mol2 becomes clozapine.mol2
#. Use: 
#. file_rename <inputfiles> -a <exp1> -b <exp2>
#  where <exp1> and <exp2> are strings or perl regular expressions
#. <exp2> is '' by default
#. Examples:
#. file_rename file_new.mol2 -a _new will rename file_new.mol2 to file.mol2
#. file_rename file.mol2 -a .mol2 -b .bak will rename file.mol2 to file.bak
#. file_rename ssss.mol2 -a 's*' -b x -re will rename ssss.mol2 to x.mol2
#. file_rename ssss.mol2 -a 's' -b x -re -s will rename ssss.mol2 to xsss.mol2
#F
#; -a_<string> string to replace
#; -b_<string> string to insert ('' by default)
#; -s Make only a single substitution of string a (not a global one)
#; -re treat string a as a regular expression
#; -clean Clean non word characters [A-Z, 0-9, - and _] from filenames
#; -renumber Convert all numbers in filenames to have leading zeros
#; -force overwrite existing files
#. $Revision: 1.22.2.1.2.1 $
#>

use strict;
package Silico;

#########################
# Variable declarations #
#########################

my $arg;
my @arguments;
my $count = 0;
my $force;
my $starttime = (times)[0];

###################
# Start of script #
###################

silico_setup();
print_title("file_rename", '$Revision: 1.22.2.1.2.1 $');

my $a  = make_flag('a', undef, 'String to replace', 1, '');
my $b  = make_flag('b', undef, 'String to insert', 1, '');
my $s  = make_flag('s', undef, 'Make only a single substitution (not global)');
my $clean = make_flag('clean', undef, 'Clean non word characters [A-Z, 0-9, - and _] from filenames');
my $renumber = make_flag('renumber', undef, 'Convert numbers in filenames with less than three digits to have leading zeros');
my $re = make_flag('re', undef, 'Treat string "a" as a regular expression');

$force = make_flag('force', 'force', 'Force overwriting of output files');

@arguments = &get_arguments2('>=1');

silico_msg ('d', "You must set at least one flag (-a, -clean, -renumber)\n") if ($a ne '0' && !$a) && !$clean && !$renumber;

print "\n";

# Literal substitution

if (!$re) {
	my @f = split '', $a;
	my $f;
	foreach $f (@f) {
		foreach (qw !^ * $ . \ / [ ] ( )! ) {
			next if $f ne $_;
			$f = "\\$f";
		}
	}
	$a = join '', @f;
}

print "Using regular expression '$a' and replacing with '$b'\n\n";

foreach $arg (@arguments) {
	
	my $ext = get_fileext($arg);
	my $dir = get_file_dir($arg);
	my $newname = get_filebase_short($arg);
	$newname .= ".$ext" if $ext;

	if ($s) {
		$newname =~ s/$a/$b/;
	} else {
		$newname =~ s/$a/$b/g;
	}
	
	if ($clean) {
		# Replace spaces and nonwords with underscores 
		$newname =~ s/[^\w.]+/_/g;
		$newname =~ s/_+/_/g;
		$newname =~ s/_\././g;
		$newname =~ s/\._/./g;
		$newname =~ s/^_//;
		$newname =~ s/_$//;
	}
	
	if ($renumber) {
		# Renumber files with numbers less than 3 digits long with two leading zeros 
		$newname  =~ s/((?<=\D)(?=\d\D))/00/g;
		$newname  =~ s/((?<=\D)(?=\d\d\D))/0/g;
	}
	
	$newname = $dir."/".$newname if $dir;
		
	# Skip if there was no change
	next if $newname eq $arg;	
	
	if (!$force && -e $newname) {
		silico_msg("v", "Output file '$newname' already exists. Skipping.\n",
				"Use '-force' to overwrite.\n\n");
		next;
	}
	
	my $run =  "mv '$arg' '$newname'";
	system ($run) == 0 || silico_msg('d', "Command \"$run\" failed!\n");
	++$count;
}

silico_msg('c', "Modified name of $count files\n");

print_finished_message();
print_timing_message($starttime);

#################
# End of script #
#################






###############
# Subroutines #
###############

sub silico_setup {

        #<
        #? Locate the Silico libraries and read in the parent library, silico.pm
        #; Sets: $Silico::home_dir, $Silico::lib_dir
        #; Requires: nothing
        #; Returns: nothing
        #>

        die "Error: The environment variable SILICO_HOME is not set\n" if !$ENV{SILICO_HOME};

        $Silico::home_dir = $ENV{SILICO_HOME}."/";
        $Silico::lib_dir = $Silico::home_dir."lib/";
        push @INC, substr($Silico::lib_dir, 0, -1);

        require silico;
}
