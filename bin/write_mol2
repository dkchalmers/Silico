#!/usr/bin/perl -w

#  COPYRIGHT NOTICE
#
#  Silico - A Perl Molecular Toolkit
#  Copyright (C) 2008-25 David K. Chalmers and Benjamin P. Roberts,
#  Department of Medicinal Chemistry, Monash University
#
#  This program is free software: you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with this program.  If not, see <http://www.gnu.org/licenses/>.

#<
#! write_mol2
#? Read any silico format and write a mol2 file
#! Use
#. write_mol2 <file1> <file2> ...
#F
#; -r Rename molecules using SDF data field for conversion from SDF to mol2
#; -mm Write output in MolMol Mol2 format.
#; -p Write output in Mol2 Protein format.
#; -dr Print additional debugging information for rings
#; --nosingle Do not use single molecule read/write routines
#SF
#. $Revision: 1.15-21-g2cee521 $
#. Created: DKC November 1999 and later.
#>

use strict;

package Silico;

#########################
# Variable declarations #
#########################

my $options;
my $starttime = (times)[0];

###################
# Start of script #
###################

silico_setup();
print_title("write_mol2", '$Revision: 1.15-21-g2cee521 $');

my $rename = make_flag('r', 'rename', 'Rename molecules using SDF data field');
add_option('MOLMOL',  $options) if make_flag('mm', 'molmol-format',  'Write output in MolMol small molecule format');
add_option('PROTEIN', $options) if make_flag('p',  'protein-format', 'Write output in protein format');
my $nosingle    = make_flag(undef, 'nosingle',    'Do not use single molecule read/write routines');

my @arguments = get_arguments2('>=1');

foreach my $arg (@arguments) {

	print "File: $arg\n" if (!get_flag('quiet', 'l'));

	if (!$nosingle) {

		my $fr_in;
		my $fr_out;

		while (my $mol = read_mol_single($fr_in, $arg)) {

			#fr_printout($fr_in, 'write_mol2');

			# Rename using SDF data field is here so that we can convert
			# from SDF to Mol2 and transfer the molecule name in one step
			if ($rename) {

				if (!defined $mol->{SDF_DATA}{$rename}) {
					silico_msg('w', "SDF data $rename is not defined!\n", "Setting molecule name to \"Mol\".\n");
					$mol->{NAME} ||= 'Mol';
				} else {
					$mol->{NAME} = $mol->{SDF_DATA}{$rename};
				}
			}

			write_mol_single($fr_out, $mol, undef, 'mol2');
		}

		close_molfile($fr_out);

	} else {

		my $mols = read_mol_any($arg);
		next if (!defined $mols);

		foreach my $mol (@$mols) {

			# Rename using SDF data field is here so that we can convert
			# from SDF to Mol2 and transfer the molecule name in one step
			if ($rename) {

				if (!defined $mol->{SDF_DATA}{$rename}) {
					silico_msg('w', "SDF data $rename is not defined!\n", "Setting molecule name to \"Mol\".\n");
					$mol->{NAME} ||= 'Mol';
				} else {
					$mol->{NAME} = $mol->{SDF_DATA}{$rename};
				}
			}

			$Silico::debug && mol_check($mol);
			$Silico::debug && mol_check_valences($mol, 1);    # Molecule may not have hydrogens
		}

		write_mol_any($mols, undef, 'mol2', $options);

		ensemble_printout($mols, 'all') if $Silico::debug;
	}
}

print_finished_message();
print_timing_message($starttime);

#################
# End of script #
#################

###############
# Subroutines #
###############

sub silico_setup {

	#<
	#? Locate the Silico libraries and read in the parent library, silico.pm
	#; Sets: $Silico::home_dir, $Silico::lib_dir
	#; Requires: nothing
	#; Returns: nothing
	#>

	die "Error: The environment variable SILICO_HOME is not set\n" if !$ENV{SILICO_HOME};

	$Silico::home_dir = $ENV{SILICO_HOME} . "/";
	$Silico::lib_dir  = $Silico::home_dir . "lib/";
	push @INC, substr($Silico::lib_dir, 0, -1);

	require silico;
}
